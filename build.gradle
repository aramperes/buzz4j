import org.gradle.internal.os.OperatingSystem

plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

group 'dev.poire.buzz4j'
version '1.0.0'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
}

test {
    def libDir = file("${project('buzz4jni').buildDir}/lib/main/debug")
    def hbLibDir = file("${rootDir}/harfbuzz/build/src")

    def os = OperatingSystem.current();
    if (os.isWindows()) {
        def freeTypeDir = file("${rootDir}/harfbuzz/build/subprojects/freetype-2.13.0")
        def pngDir = file("${rootDir}/harfbuzz/build/subprojects/libpng-1.6.39")

        jvmArgs = ["-Djava.library.path=${pngDir.absolutePath};${freeTypeDir.absolutePath};${hbLibDir.absolutePath};${libDir.absolutePath}"]
    } else if (os.isLinux()) {
        jvmArgs = ["-Djava.library.path=${hbLibDir.absolutePath}:${libDir.absolutePath}"]
    } else {
        // macOS support?
    }

    useJUnitPlatform()
}

test.dependsOn ':buzz4jni:linkDebug'

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'buzz4j'
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'buzz4j'
                description = 'Java bindings for HarfBuzz, a text-shaping library.'
                url = 'https://github.com/aramperes/buzz4j'
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://raw.githubusercontent.com/aramperes/buzz4j/main/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = 'aramperes'
                        name = 'Aram Peres'
                        email = 'aram.peres@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/arampres/buzz4j.git'
                    developerConnection = 'scm:git:https://github.com/arampres/buzz4j.git'
                    url = 'https://github.com/arampres/buzz4j'
                }
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
            def snapshotsRepoUrl = 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl

            credentials(PasswordCredentials) {
                username = ossrhUsername
                password = ossrhPassword
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}
