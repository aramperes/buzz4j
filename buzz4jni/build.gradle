import org.gradle.internal.jvm.Jvm

plugins {
    id 'cpp-library'
}

group 'dev.poire.buzz4j'
version '1.0-SNAPSHOT'

library {
    baseName = 'buzz4j'

    binaries.configureEach { CppSharedLibrary binary ->
        def compileTask = binary.compileTask.get()
        def linkTask = binary.linkTask.get()
        compileTask.includes.from("${Jvm.current().javaHome}/include", "${rootDir}/harfbuzz/src")

        def osFamily = binary.targetPlatform.targetMachine.operatingSystemFamily
        if (osFamily.macOs) {
            compileTask.includes.from("${Jvm.current().javaHome}/include/darwin")
        } else if (osFamily.linux) {
            compileTask.includes.from("${Jvm.current().javaHome}/include/linux")
        } else if (osFamily.windows) {
            compileTask.includes.from("${Jvm.current().javaHome}/include/win32")
        }

        compileTask.source.from fileTree(dir: "src/main/cpp", include: "**/*.cc")

        def toolChain = binary.toolChain
        def hbLibDir = file("${rootDir}/harfbuzz/build/src")
        if (toolChain instanceof VisualCpp) {
            def hbLib = file("${hbLibDir}/harfbuzz.lib")
            linkTask.linkerArgs.addAll(["/LIBPATH:${hbLibDir.absolutePath}", hbLib.absolutePath])
        } else if (toolChain instanceof GccCompatibleToolChain) {
            def hbLib = "harfbuzz"
            linkTask.linkerArgs.addAll(["-L${hbLibDir.absolutePath}", "-l${hbLib}"])
        }
    }
}
